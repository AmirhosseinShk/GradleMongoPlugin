import org.gradle.api.tasks.wrapper.Wrapper.DistributionType

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.9.9'
    id 'groovy'
    id 'idea'
    id 'eclipse'
    id 'maven'
    id 'signing'
    id 'java-gradle-plugin'
}

archivesBaseName = 'gradle-mongo-plugin'
group = 'com.sourcemuse.gradle.plugin'
version = '1.0.4-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.0.0'
    compile 'org.mongodb:mongo-java-driver:3.6.0'

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', {
        exclude module: 'groovy-all'
    }
    testCompile 'org.littleshoot:littleproxy:1.1.2'
}

task wrapper(type: Wrapper) {
    distributionType = DistributionType.ALL
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required { !version.endsWith('SNAPSHOT') }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { signing.signPom(it) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: getSonatypeUsername(), password: getSonatypePassword())
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: getSonatypeUsername(), password: getSonatypePassword())
            }

            pom.project {
                name 'Gradle Mongo Plugin'
                packaging jar
                description 'Gradle plugin for managing a local instance of MongoDb'
                url 'https://github.com/sourcemuse/GradleMongoPlugin'

                scm {
                    url 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                    connection 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                    developerConnection 'scm:git@github.com:sourcemuse/GradleMongoPlugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name 'Mashooq Badar'
                    }
                    developer {
                        name 'Robert Taylor'
                    }
                }
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/sourcemuse/GradleMongoPlugin'
    vcsUrl = 'https://github.com/sourcemuse/GradleMongoPlugin'
    description = 'Gradle plugin for running a managed instance of Mongo.'
    tags = ['mongo', 'mongodb']

    plugins {
        mongoPlugin {
            id = 'com.sourcemuse.mongo'
            displayName = 'Gradle Mongo plugin'
        }
    }
}

gradlePlugin {
    plugins {
        mongoPlugin {
            id = 'com.sourcemuse.mongo'
            implementationClass = 'com.sourcemuse.gradle.plugin.flapdoodle.gradle.GradleMongoPlugin'
        }
    }
}

String getSonatypeUsername() {
    properties['sonatypeUsername'] ?: 'username'
}

String getSonatypePassword() {
    properties['sonatypePassword'] ?: 'password'
}
